@tool
class_name GaeaNodeFlagsMapper
extends GaeaNodeMapper
## Maps every cell of [param reference] that matches the flag conditions to [param material].
##
## Flags are [code]int[/code]s, so the filtering is done with the rounded value
## of each cell of [param reference], using a bitwise [code]AND[/code].[br]
## If [param match_all] is [code]false[/code], the value has to pass the filter for only
## one of the flags in [param match_flags] to be mapped.[br]
## If a value matches [b]any[/b] of the [param exclude_flags], the cell's excluded from the output.


func _get_title() -> String:
	return "FlagsMapper"


func _get_description() -> String:
	return "Maps every cell of [param reference] that matches the flag conditions to [param material]."


func _get_arguments_list() -> Array[StringName]:
	return super() + ([&"match_all", &"match_flags", &"exclude_flags"] as Array[StringName])


func _get_argument_type(arg_name: StringName) -> GaeaValue.Type:
	match arg_name:
		&"match_all":
			return GaeaValue.Type.BOOLEAN
		&"match_flags":
			return GaeaValue.Type.FLAGS
		&"exclude_flags":
			return GaeaValue.Type.FLAGS
	return super(arg_name)


func _passes_mapping(
	reference_sample: GaeaValue.Sample, cell: Vector3i, args: Dictionary[StringName, Variant]
) -> bool:
	var match_all: bool = args.get(&"match_all")
	var flags: Array = args.get(&"match_flags")
	var exclude_flags: Array = args.get(&"exclude_flags")

	var value: float = reference_sample.get_cell(cell)
	var matches_excluded_flags := exclude_flags.any(_matches_flag.bind(value))
	if match_all:
		var matches_all_flags := flags.all(_matches_flag.bind(value))
		return matches_all_flags and not matches_excluded_flags

	var matches_any_flags := flags.any(_matches_flag.bind(value))
	return matches_any_flags and not matches_excluded_flags


func _matches_flag(value: float, flag: int) -> bool:
	return roundi(value) & flag
